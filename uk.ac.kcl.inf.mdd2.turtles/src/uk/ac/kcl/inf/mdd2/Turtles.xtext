grammar uk.ac.kcl.inf.mdd2.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/mdd2/Turtles"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtlesProgram:
	statements += Statement *
;


Statement:
	MoveStatement | TurnStatement | LoopStatement | VariableDeclarationStatement
;

VariableDeclarationStatement:
	'var' name=ID '=' value=AnyInt
;

LoopStatement:
	times=AnyInt 'times' 'do'
		statements += Statement*
	'end'
;

MoveStatement:
	dir=MoveDirection '(' dist=AnyInt ')'
;

TurnStatement:
	'turn' dir=TurnDirection 'by' deg=REAL 'degrees'
;

enum MoveDirection:
	forward | backward
;

enum TurnDirection:
	left | right
;

Addition returns Expression:
  Multiplication ({Addition.left=current} '+' right=Multiplication)*;
  
Multiplication returns Expression:
  Primary ({Multiplication.left=current} '*' right=Primary)*;
  
Primary returns Expression:
  NumberLiteral |
  '(' Addition ')';
  
NumberLiteral:
  value=INT;

REAL returns ecore::EFloat hidden():
	INT? '.' INT
;
AnyInt:
	val=INT
	| ref=[VariableDeclarationStatement]
	| exp=Addition
;